(ns kotoba.wasm.compat-api-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.wasm.dom :as dom]
            [re-frame.core :as rf]
            [reagent.core :as r]))

(deftest re-frame-compat-supports-query-args-and-sync-dispatch
  (rf/clear!)
  (rf/reg-event-db :init (fn [_ [_ value]] {:items value}))
  (rf/reg-event-db :append (fn [db [_ value]] (update db :items conj value)))
  (rf/reg-sub :item-count (fn [db [_ multiplier]]
                            (* multiplier (count (:items db)))))
  (rf/dispatch-sync [:init [:a :b]])
  (rf/dispatch [:append :c])
  (is (= 6 @(rf/subscribe [:item-count 2]))))

(deftest re-frame-clear-removes-handlers-and-subscriptions
  (rf/clear!)
  (rf/reg-event-db :init (fn [_ _] {:ready? true}))
  (rf/reg-sub :ready? (fn [db _] (:ready? db)))
  (rf/dispatch-sync [:init])
  (is (true? @(rf/subscribe [:ready?])))
  (rf/clear!)
  (testing "event handlers are reset"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"No event-db handler"
                          (rf/dispatch-sync [:init]))))
  (testing "subscription handlers are reset"
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"No subscription"
                          @(rf/subscribe [:ready?])))))

(deftest reagent-compat-atom-and-as-element-are-portable-shims
  (let [state (r/atom 1)
        hiccup [:span.value {:class [:hot nil :wide]} @state]
        rendered (r/render (r/as-element hiccup))]
    (swap! state inc)
    (is (= 2 @state))
    (is (= "1" (dom/text-content (:document rendered))))
    (is (= "value hot wide"
           (->> (get-in rendered [:document :nodes])
                vals
                (some #(when (= :span (:tag %))
                         (get-in % [:attrs :class]))))))))
